include_guard(GLOBAL)

function(detri_resolve_dependency)
    set(options)
    set(one_value_args TARGET PACKAGE FETCHCONTENT_NAME)
    set(multi_value_args FIND_PACKAGE_ARGS FETCHCONTENT_DECLARE_ARGS)
    cmake_parse_arguments(DRD "${options}" "${one_value_args}" "${multi_value_args}" ${ARGN})

    if (NOT DRD_TARGET)
        message(FATAL_ERROR "detri_resolve_dependency requires TARGET <target>")
    endif()

    if (TARGET ${DRD_TARGET})
        return()
    endif()

    if (DRD_PACKAGE)
        if (DRD_FIND_PACKAGE_ARGS)
            find_package(${DRD_PACKAGE} CONFIG QUIET ${DRD_FIND_PACKAGE_ARGS})
        else()
            find_package(${DRD_PACKAGE} CONFIG QUIET)
        endif()
    endif()

    if (TARGET ${DRD_TARGET})
        return()
    endif()

    if (PROJECT_IS_TOP_LEVEL AND DRD_FETCHCONTENT_NAME)
        if (NOT DRD_FETCHCONTENT_DECLARE_ARGS)
            message(FATAL_ERROR "detri_resolve_dependency requires FETCHCONTENT_DECLARE_ARGS when FETCHCONTENT_NAME is provided")
        endif()

        include(FetchContent)
        FetchContent_Declare(${DRD_FETCHCONTENT_NAME} ${DRD_FETCHCONTENT_DECLARE_ARGS})
        FetchContent_MakeAvailable(${DRD_FETCHCONTENT_NAME})
    endif()

    if (NOT TARGET ${DRD_TARGET})
        set(_detri_error "Failed to resolve dependency target '${DRD_TARGET}'.")
        if (DRD_PACKAGE)
            string(APPEND _detri_error " Tried find_package(${DRD_PACKAGE} CONFIG QUIET).")
        endif()
        if (PROJECT_IS_TOP_LEVEL)
            if (DRD_FETCHCONTENT_NAME)
                string(APPEND _detri_error " Top-level FetchContent fallback also failed for '${DRD_FETCHCONTENT_NAME}'.")
            endif()
        else()
            string(APPEND _detri_error " Add the dependency in the parent project (target), install a package for find_package, or configure this repo standalone.")
        endif()
        message(FATAL_ERROR "${_detri_error}")
    endif()
endfunction()